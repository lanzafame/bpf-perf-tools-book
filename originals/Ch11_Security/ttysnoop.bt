#!/usr/bin/env bpftrace
/*
 * ttysnoop - Mirror tty output.
 *
 * See BPF Performance Tools, Chapter 11, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 26-Feb-2019  Brendan Gregg   Created this.
 */

#include <linux/fs.h>

BEGIN
{
	if ($1 == 0) {
		printf("USAGE: ttysnoop.bt pts_device    # eg, pts14\n");
		exit();
	}
	printf("Tracing tty writes. Ctrl-C to end.\n");
}

kprobe:tty_write
{
	$file = (struct file *)arg0;
	// +3 skips "pts":
	if (str($file->f_path.dentry->d_name.name) == str($1 + 3)) {
		printf("%s", str(arg1, arg2));
	}
}
