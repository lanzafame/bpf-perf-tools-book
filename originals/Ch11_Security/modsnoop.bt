#!/usr/bin/env bpftrace
/*
 * modsnoop - Show kernel module loads.
 *
 * See BPF Performance Tools, Chapter 11, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 14-Mar-2019  Brendan Gregg   Created this.
 */

#include <linux/module.h>

BEGIN
{
	printf("Tracing kernel module loads. Hit Ctrl-C to end.\n");
}


kprobe:do_init_module
{
	$mod = (struct module *)arg0;
	time("%H:%M:%S ");
	printf("module init: %s, by %s (PID %d, user %s, UID %d)\n",
	    $mod->name, comm, pid, username, uid);
}
